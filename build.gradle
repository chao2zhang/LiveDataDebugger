plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
    id 'org.jetbrains.dokka' version '1.4.30'
    id 'java-gradle-plugin'
    id 'com.vanniktech.maven.publish' version '0.13.0'
}

repositories {
    google()
    mavenCentral()
    jcenter {
        content {
            includeGroup "org.jetbrains.trove4j"
        }
    }
}

gradlePlugin {
    plugins {
        loggingLiveDataPlugin {
            id = 'io.github.chao2zhang.logginglivedata'
            implementationClass = "io.github.chao2zhang.LoggingLiveDataPlugin"
        }
    }
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.31"
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-reflect:1.4.31") {
            because("Android Gradle Plugin 4.1 depends on Kotlin 1.3.72 but we should not mix 1.3 and 1.4.")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.31") {
            because("Android Gradle Plugin 4.1 depends on Kotlin 1.3.72 but we should not mix 1.3 and 1.4.")
        }
    }
    implementation 'com.android.tools.build:gradle-api:4.1.3'
    implementation 'org.ow2.asm:asm:9.1'
    compileOnly 'com.android.tools.build:gradle:4.1.3'
}
